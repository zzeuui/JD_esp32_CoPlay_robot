## ESP32 모터 제어 

### JD 로봇 플랫폼의 4바퀴로봇 소개 
제이디에듀의 JD 로봇 플래폼은 ESP32 기반으로 로봇 제작 하드웨어 소프트웨어를 학습할 수 있도록 다양한 커리큘럼을 제공해 주는 키트 입니다. JD 로봇 플랫폼은 로봇을 학습할 수 있은 여러가지 로봇키트를 제공합니다. 이 과정에서 사용하는 4바퀴로봇은 로봇 하드웨어 소프트웨어 중에서 다음 사항을 학습할 수 있는 바퀴 구동 소형 로봇 키트 입니다. 

- 로봇 바디 설계, 제작, 조립
- 모터 제어
- 거리센서, 자이로센서, 가속도센서 제어
- 로봇 장애물 회피 알고리즘
- 간이 자율주행 알고리즘
- 웹으로 로봇을 제어하기
- 블루투스로 로봇 제어하기
- 로봇 리모트 컨트롤러 하드웨어 만들어 보기
- 웹 영상 전송, 오브젝트 디텍션

제어보드의 회로도는 [여기](https://github.com/JD-edu/JD_robot_platform/blob/main/%EC%BB%A4%EB%A6%AC%ED%81%98%EB%9F%BC%EB%AC%B8%EC%84%9C/%EC%A0%9C%EC%96%B4%EB%B3%B4%EB%93%9C_%EC%8A%A4%ED%82%A4%EB%A9%94%ED%8B%B1.pdf)를 참고합니다. 

4바퀴로봇의 커리큘럼은 기본과정과 응용과제로 나누어져 있습니다.

### ESP32 제어보드 구성 
4바퀴로봇의 제어보드는 ESP32 DevKit과 확장보드로 구성되어 있습니다. 여기서는 제어보드의 구성을 설명하겠습니다. 제어보드는 ESP32 DevKit과 확장보드로 구성되어 있다. 

#### 제어보드의 기능
제어보드의 IO 기능은 다음과 같습니다. 

|IO|내용|비고|
|------|-----------|------|
|IO|7개의 IO, 디지털 IO, PWM 포트 ||
|DC모터 포트 |4개의 DC모터 포트 |N20 RPM 50권장|
|I2C 포트|3개의 I2C 포트 ||
|전원공급 |카메라 전원 공급 커넥터(5V)||

2.54 핀헤더로 구성되어 있는 IO는 7개가 있습니다. 디지털 입출략, PWM 출력 등이 가능합니다. PWM이 지원 되므로 각 포트에 모두 서보모터를 동작시킬 수 있습니다. DC모터 포트는 모두 4개가 있습니다. 동시에 공급할 수 있는 전류의 한계가 있으므로 N20 모터 RPM50 4개를 구동하는 것을 권장 합니다. I2C 포트는 3개가 제공됩니다. 끝으로 카메라를 4바퀴로봇에 사용할 경우, 카메라 전원이 필요한데 카메라 전원용 5V를 제공합니다.  

#### 제어보드의 구성 
ESP32 제어보드의 구성은 다음 사진을 참고합니다.

![제어보드_이미지](https://github.com/JD-edu/JD_robot_platform/assets/96219601/3203443d-775c-42b2-aec7-f1d33b83e176)

- 1: 7개의 IO 중 4개 IO
- 2: 7개의 IO 중 3개 IO
- 3: I2C 버스 3개
- 4: 카메라 전원 공급용 5V
- 5: 4개의 모터 포트
- 6: 모터 공급 전원 선택 스위치 (5V, 7.4V)
- 7: 배터리 포트 (7.4V 직렬 18650 충전배터리)
- 8: 제어보드 전원 스위치 

### 모터제어 해보기 
이번에는 조립된 4바퀴로봇을 코딩해서 모터를 제어해 보도록 하겠습니다. 모터를 제어하는 방식은 시리얼 포트를 통해서 PC에서 명령을 내리면 이 명령을 4바퀴로봇의 ESP32 제어보드가 받아서 모터를 제어하는 방식입니다.  깃허브에서 다운로드 받은 소스코드 중에서 "102_esp32_motor_control" 폴더에 102_esp32_motor_control.ino 소스코드 파일을 아두이노 IDE ***파일 -> 열기***  로 엽니다. 이 소스코드는 4바퀴로봇에 연결되어 있는 4개의 모터를 제어할 수 있도록 해 줍니다. 

#### setup() 함수 
setup()함수에서는 시리얼을 115200 속도로 통신할 수 있도록 열어 줍니다. 그리고 시작 메시지를 출력합니다. 

```C
    Serial.begin(115200);
    Serial.print("motor test start...");
```

그 다음에는 pinMode() 함수를 시용하여 모터를 제어하는 IO들을 출력으로 셋팅합니다. 4바퀴로봇은 모두 8개의 IO를 사용합니다. 1개의 모터를 전진 후진 제어를 하기위해서는 2개의 IO가 필요해서 모두 8개의 IO가 필요합니다. 모터제어 사용되는 IO는 숫자보다는 #define 을 사용해서 이름을 정의해서 사용합니다. 

```C

    #define M1_B    26
    #define M1_A    27
    #define M2_B    12
    #define M2_A    14
    #define M3_B    15
    #define M3_A    2
    #define M4_B    4
    #define M4_A    16

    pinMode(M1_A, OUTPUT);
    pinMode(M1_B, OUTPUT);
    pinMode(M2_A, OUTPUT);
    pinMode(M2_B, OUTPUT);
    pinMode(M3_A, OUTPUT);
    pinMode(M3_B, OUTPUT);
    pinMode(M4_A, OUTPUT);
    pinMode(M4_B, OUTPUT);
```

#### 모터제어 함수 
코드를 잘 정리하기 위해서 모터제어 코드들은 함수로 묶습니다. 모터제어 함수는 전진, 후진, 좌회전, 우호전, 정지 기능이 있습니다. 
4개의 모터를 모두 전진하는 방향으로 회전시키려면 다음과 같이 digitalWrite()함수를 사용합니다. 

```C
void go_forward(){
  Serial.println("forward");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, HIGH);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, HIGH);

  digitalWrite(M3_A, HIGH);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, HIGH);
  digitalWrite(M4_B, LOW);
}
```
모터제어용 IC는 2개의 ESP32 IO를 사용하는데 예를 들어 모터1번은 M1_A, M1_B 두개의 신호를 사용합니다. 모터는 다음표와 같아 제어 됩니다. 

|모터1 동작|M1_A|M1_B|
|------|---|---|
|전진|LOW|HIGH|
|후진|HIGH|LOW|
|멈춤|LOW|LOW|

비슷한 방식으로 4바퀴로봇의 후진함수는 다음과 같이 구현할 수 있습니다. go_forward() 함수와 go_backward() 함수는 코드들이 대칭 구조를 가지고 있습니다.  

```C
void go_backward(){
  Serial.println("backward");
  digitalWrite(M1_A, HIGH);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, HIGH);
  digitalWrite(M2_B, LOW);

  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, HIGH);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, HIGH);
}
```

4바퀴로봇은 좌회전/우회전의 경우, 전차와 비슷한 방식으로 동작합니다. 좌회전 할때는  좌측 바퀴 2개는 후진 시키고 우측 바퀴 2개는 전진 시키면 로봇은 좌호전 하게 됩니다, 우회전의 경우는 반대로 합니다. 4바퀴로봇의 좌회전과 우회전은 다음과 같이 구현합니다. 

```C
void turn_right(){
  Serial.println("right");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, HIGH);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, HIGH);

  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, HIGH);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, HIGH);
}

void turn_left(){
  Serial.println("left");
  digitalWrite(M1_A, HIGH);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, HIGH);
  digitalWrite(M2_B, LOW);

  digitalWrite(M3_A, HIGH);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, HIGH);
  digitalWrite(M4_B, LOW);
}
```
모터 멈춤은 다음과 같이 구현합니다. 모든 IO를 LOW로 하면 4개 모터 모두 멈추게 됩니다. 

```C
void stop(){
  Serial.println("stop");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, LOW);
  
  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, LOW);
  delay(200);
}
```
#### 시리얼 데이터 수신 코드 구현 
이번 단계에서는 PC 시리얼 통신을 통해서 4바퀴로봇을 조종합니다. 아두이노 IDE에 내장된 시리얼 모니터는 ESP32가 보내는 문자 데이터를 디스플레이 하는 기능도 있지만, PC에서 문자 데이터를 ESP32로 보내는 기능도 있습나다. PC 시리얼로 4바퀴로봇을 조종하기 위해서는 먼저 4바퀴로봇과 PC를 시리얼로 연결을 합니다. 그리고 프로그램 업로드 할대와 마찬가지고 ***도구 -> 포트*** 에서 ESP32에 연결돈 포트를 셋팅하야 합니다.  PC에서 전달되는 문자 데이터를 수신하는 코드는 다음과 같습니다.

```C
if(Serial.available() > 0){
    char c = Serial.read();
    Serial.println(c);
    if(c == 'f'){
      go_forward();
    }else if(c == 'b'){
      go_backward();
    }else if(c == 'l'){
      turn_left();
    }else if(c == 'r'){
      turn_right(); 
    }else if(c == 'p'){
      stop();
    }
} 
```
Serial.availabel() 함수는 ESP32의 시리얼버퍼라에 문자 데이터가 있는지를 알려줍니다. Serial.begin() 함수를 실행할 때, ESP32 내부에 소프트웨어적으로 시리얼 버퍼가 생성 됩니다. PC에서 전달된 데이터는 이 시리얼버퍼에 임시로 저장이 됩니다. Serial.available() 함수는 이 시리얼버퍼에 저장된 데이터가 몇 바이트인지 알려줍니다. Serial.available() 함수는 [여기](https://www.arduino.cc/reference/ko/language/functions/communication/serial/available/)를 참고합니다. 

```c
if(Serial.available() > 0){
    ...
}
```
위 코드에서 PC에서 전달된 문자 데이터가 시리얼버퍼에 있다면 if문 내부의 코드들이 실행하게 됩니다. 시리얼버퍼에 데이터가 없다면 Serial.available()의 리턴값이 0이 됩니다. 시리얼버퍼에 저장된 문자 데이터를 읽기 위해서는 다음 코드를 사용합니다.

```C
char c = Serial.read();
```

이 코드는 시리얼버퍼에 있는 문자 데이터 하나를 읽어서 변수 c에 저장합니다. Serial.read()함수의 상세한 사항은 [여기](https://www.arduino.cc/reference/ko/language/functions/communication/serial/read/)를 참고 할수 있습니다. 시리얼버퍼에 문장('\n로 끝이나는)이 저장되어 있다면 Serial.readStrngUntil()함수를 사용할 수 있습니다. 이 함수는 [여기](https://www.arduino.cc/reference/en/language/functions/communication/serial/readstringuntil/)를 참고하면 됩니다.
PC 시리얼 통신으로 4바퀴로봇을 제어하기 위해서는 먼저 간단한 프로토콜을 정해야 합니다. 어떤 문자 데이터를 받으면 전진을 하고, 어던 문자를 받으면 후진을 하고 등을 정해야 합니다. 현재는 다음과 같이 간단하게 프로토콜을 정의했습니다. 

|로봇 동작|문자 코드|
|------|---|
|전진|'f'|
|후진|'b'|
|좌회전|'l'|
|우회전|'r'|
|멈춤|'p'|

그리고 이것을 처리하는 코드는 다음과 같습니다. 
```C
if(c == 'f'){
      go_forward();
    }else if(c == 'b'){
      go_backward();
    }else if(c == 'l'){
      turn_left();
    }else if(c == 'r'){
      turn_right(); 
    }else if(c == 'p'){
      stop();
    }
```
이제 코딩을 완료했으면 코드를 4바퀴로봇에 업로드하고 동작을 시켜보도록 합시다. 업로드 과정은 이전 강좌를 참고할 수 있습니다. 여기서는 문자 데이터를 PC를 통해서 ESP32로 보내는 방법을 알려드립니다. 먼저 아두이노 IDE에서 ***도구 -> 시리얼 모니터***  메뉴를 통해서 시리얼모니터를 실행합니다. 시리얼모니터는 두 부분으로 되어 있고, 노란색 원부분을 통해서 문자 데이터를 PC에서 ESP32로 보낼 수 있습니다. 

![esp32_데이터전송](https://github.com/JD-edu/JD_robot_platform/assets/96219601/c359dbf6-6a92-4c90-bfd5-10a9ebf6c5e7)

이제 'f'문자를 입력하고 엔터키를 입력해 봅니다. 4바퀴로봇이 전진동작을 수행할 것 입니다. 만약 동작이 되지 않는다면 다음 사항들을 점검해 봅니다.

- 시리얼모니터의 속도가 115200인지 확인 합니다.
- 4바퀴로봇이 시리얼포트를 통해서 PC에 연결되어 있는지? 포트 설정이 제대로 되어 있는지 확인합니다.
- 코드가 제대로 동작하는지 의심되는 코드 곳곳에 Serial.println() 코드를 넣어서 동작을 확인합니다. 



