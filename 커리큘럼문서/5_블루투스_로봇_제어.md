## 블루투스로 로봇 제어해보기 

###  ESP32 블루트스로 로봇을 제어하기 
ESP#@ 마이크로컨트롤로는 블르투스 기능을 내장하고 있습니다. 그래서 ESP32 마이크로컨트롤러를 스마트폰 혹은 PC를 이용해서 블루투스로 제어하는 것이 가능합니다. 이번에는 ESP32 기반 4바퀴로봇을 블루투스로 원격조종하는 것을 해 보겠습니다. 

#### 스마트폰 앱의 준비
블루투스에는 블루투스 클래식이라고 부르는 기존 블루투스 기능이 있고, BLE(Bluetooth Low Energy)라고 부르는 저전력 버전의 블루투스가 있습니다. 블루투스에 대한 상세한 내용은 [여기](https://www.bluetooth.com/ko-kr/)를 참고합니다. BLE에 대한 상세한 사항은 [여기](https://velog.io/@zhemdrawer/BLE-%EC%9D%B4%ED%95%B4%ED%95%98%EA%B8%B0)를 참고합니다. ESP32 마이크로컨트럴러는 블루투스 클래식과 BLE 모두를 지원합니다. 하지만 우리는 블루투스 클래식을 사용해서 4바퀴로봇을 제어합니다.   
스마트폰으로 ESP3를 불루투스 제어하려면, 스마트폰 앱을 설치해야 합니다. 블루투스 클래식으로 ESP32같은 디바이스와 통신하는 무료앱은 많이 구글플레이에 등록이 되어 있습니다. 아이폰은 불루투스 클래식 사용이 제한적이라 다루지 않습니다.우리가 사용할 스마트폰앱은 다음 앱입니다. 이름은 **안드로이드 블루투스 시리얼 터미널** 입니다.  

![android_blutooth_serial_terminal](https://github.com/JD-edu/JD_robot_platform/assets/96219601/73dcc1b0-66a2-4735-8477-a8a50ed88f9b)

블루투스에는 여러가지 기능이 있는데 그 중에서 무선으로 시리얼포트를 사용하게 해주는 기능이 있습니다. 이 기능을 이용해서 스마트폰에서 특정 문자를 무선으로 ESP32에 전송할 수 있습니다. 그러면 시리얼포트오 제어문자를 보내서 4바퀴로못을 제어하는 것과 비슷하게 로봇을 무선제어할 수 있습니다. 

#### 스마트폰앱의 설치 및 준비 


### 스마트폰으로 4바퀴로봇 무선 제어하기 
스마트폰의 앱이 준비가 되었으므로 ESP32를 코딩해 보도록 하겠습니다. 스마트폰에서 특정 문자를 전송하면 이 문자를 ESP32 제어보드가 받아서 모터를 제어하는 방식입니다. 이전 시리얼 포트 모터제어와 유선 무선의 차이가 있지만 코딩은 아주 유사합니다. 깃허브에서 다운로드 받은 소스코드 중에서 "105_bluetooth_robot_control" 폴더에 105_bluetooth_robot_control.ino 소스코드 파일을 아두이노 IDE ***파일 -> 열기***  로 엽니다. 이 소스코드는 4바퀴로봇에 연결되어 있는 4개의 모터를 제어할 수 있도록 해 줍니다. 

#### setup함수 
setup()함수는 블루투스 시리얼을 열고 스마트폰과 4바퀴로봇이 통신을 할 수 있도록 해 줍니다. setup() 함수의 전체 코드는 다음과 같습니다. 

```C
void setup() {
  Serial.begin(115200);
  SerialBT.begin("Bluetooth robot control..."); //Bluetooth device name
  Serial.println("The device started, now you can pair it with bluetooth!");
  pinMode(M1_A, OUTPUT);
  pinMode(M1_B, OUTPUT);
  pinMode(M2_A, OUTPUT);
  pinMode(M2_B, OUTPUT);
  pinMode(M3_A, OUTPUT);
  pinMode(M3_B, OUTPUT);
  pinMode(M4_A, OUTPUT);
  pinMode(M4_B, OUTPUT);
}
```

여기서 디버깅을 위해서 유선 시리얼 포트를 열어줍니다. ESP32에서 블루투스 시리얼을 사용하려면 SerialBT 기능을 사용합니다. SerialBT의 상세한 사항은 [여기](https://www.electronicwings.com/esp32/esp32-bluetooth-getting-started) 참고 합니다. SeriaBT로 블루투스 시리얼포트를 열기 위해서는 다음과 같이 코딩합니다.  

```C
  SerialBT.begin("JD 4wheel robot"); //Bluetooth device name
```

여기서 ***JD 4wheel robot***은 나중에 스마트폰에서 이 로봇을 찾을 때 사용하는 이름입니다. 블루투스와 시리얼 셋팅이 끝이 나면 모터 제어를 위해서 IO핀들을 셋팅 합니다. 4바퀴로봇은 모두 8개의 IO를 사용합니다. 1개의 모터를 전진 후진 제어를 하기위해서는 2개의 IO가 필요해서 모두 8개의 IO가 필요합니다. 모터제어 사용되는 IO는 숫자보다는 #define 을 사용해서 이름을 정의해서 사용합니다. 

```C
  pinMode(M1_A, OUTPUT);
  pinMode(M1_B, OUTPUT);
  pinMode(M2_A, OUTPUT);
  pinMode(M2_B, OUTPUT);
  pinMode(M3_A, OUTPUT);
  pinMode(M3_B, OUTPUT);
  pinMode(M4_A, OUTPUT);
  pinMode(M4_B, OUTPUT);
```

#### 모터제어 함수 
#### 모터제어 함수 
코드를 잘 정리하기 위해서 모터제어 코드들은 함수로 묶습니다. 모터제어 함수는 전진, 후진, 좌회전, 우호전, 정지 기능이 있습니다. 
4개의 모터를 모두 전진하는 방향으로 회전시키려면 다음과 같이 digitalWrite()함수를 사용합니다. 

```C
void go_forward(){
  Serial.println("forward");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, HIGH);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, HIGH);

  digitalWrite(M3_A, HIGH);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, HIGH);
  digitalWrite(M4_B, LOW);
}
```

모터제어용 IC는 2개의 ESP32 IO를 사용하는데 예를 들어 모터1번은 M1_A, M1_B 두개의 신호를 사용합니다. 모터는 다음표와 같아 제어 됩니다. 

|모터1 동작|M1_A|M1_B|
|------|---|---|
|전진|LOW|HIGH|
|후진|HIGH|LOW|
|멈춤|LOW|LOW|

비슷한 방식으로 4바퀴로봇의 후진함수는 다음과 같이 구현할 수 있습니다. go_forward() 함수와 go_backward() 함수는 코드들이 대칭 구조를 가지고 있습니다.  

```C
void go_backward(){
  Serial.println("backward");
  digitalWrite(M1_A, HIGH);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, HIGH);
  digitalWrite(M2_B, LOW);

  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, HIGH);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, HIGH);
}
```

4바퀴로봇은 좌회전/우회전의 경우, 전차와 비슷한 방식으로 동작합니다. 좌회전 할때는  좌측 바퀴 2개는 후진 시키고 우측 바퀴 2개는 전진 시키면 로봇은 좌호전 하게 됩니다, 우회전의 경우는 반대로 합니다. 4바퀴로봇의 좌회전과 우회전은 다음과 같이 구현합니다. 

```C
void turn_right(){
  Serial.println("right");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, HIGH);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, HIGH);

  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, HIGH);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, HIGH);
}

void turn_left(){
  Serial.println("left");
  digitalWrite(M1_A, HIGH);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, HIGH);
  digitalWrite(M2_B, LOW);

  digitalWrite(M3_A, HIGH);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, HIGH);
  digitalWrite(M4_B, LOW);
}
```

모터 멈춤은 다음과 같이 구현합니다. 모든 IO를 LOW로 하면 4개 모터 모두 멈추게 됩니다. 

```C
void stop(){
  Serial.println("stop");
  digitalWrite(M1_A, LOW);
  digitalWrite(M1_B, LOW);
  digitalWrite(M2_A, LOW);
  digitalWrite(M2_B, LOW);
  
  digitalWrite(M3_A, LOW);
  digitalWrite(M3_B, LOW);
  digitalWrite(M4_A, LOW);
  digitalWrite(M4_B, LOW);
  delay(200);
}
```

#### 블루투스 시리얼 데이터 수신 코드 구현 
이번 단계에서는 블루투스 시리얼 통신을 통해서 4바퀴로봇을 조종합니다. 스마트폰에서 블루투스를 통해서 전송되는 문자데이터는 ESP32에 전달 됩니다.  스마트폰에서 4바퀴로봇에 연결을 하려면 ***페어링*** 이라는 단계를 거쳐야 합니다. 페이링 방법과 로봇 제어를 위한 앱 셋팅은 이후에 설명됩니다. 스마트폰에서 전달되는 문자 데이터를 수신하는 코드는 다음과 같습니다.

```C
void loop() {
  if (SerialBT.available()) {
    char c = SerialBT.read();
    Serial.println(c);
    if(c == 'f'){
      SerialBT.println("forward");
      go_forward();
    }else if(c == 'b'){
      SerialBT.println("backward");
      go_backward();
    }else if(c == 'l'){
      SerialBT.println("turn left");
      turn_left();
    }else if(c == 'r'){
      SerialBT.println("turn right");
      turn_right(); 
    }else if(c == 'p'){
      stop();
    }
  }
}
```

SerialBT.availabel() 함수는 ESP32의 블루투스 시리얼버터에  문자 데이터가 있는지를 알려줍니다. SerialBT.begin() 함수를 실행할 때, ESP32 내부에 소프트웨어적으로 블루투스 시리얼 버퍼가 생성 됩니다. 스마트폰에서 전달된 데이터는 이 블루투스 시리얼버퍼에 임시로 저장이 됩니다. SerialBT.available() 함수는 이 시리얼버퍼에 저장된 데이터가 몇 바이트인지 알려줍니다.

```C
 if (SerialBT.available()) {
 ...
 }
```

위 코드에서 스마트폰에서 전달된 문자 데이터가 블루투스 시리얼버퍼에 있다면 if문 내부의 코드들이 실행하게 됩니다. 블루투스 시리얼버퍼에 데이터가 없다면 SerialBT.available()의 리턴값이 0이 됩니다. 블루투스 시리얼버퍼에 저장된 문자 데이터를 읽기 위해서는 다음 코드를 사용합니다.

```C
char c = SerialBT.read();
```

스마트폰 블루투스 시리얼 통신으로 4바퀴로봇을 제어하기 위해서는 먼저 간단한 프로토콜을 정해야 합니다. 어떤 문자 데이터를 받으면 전진을 하고, 어던 문자를 받으면 후진을 하고 등을 정해야 합니다. 현재는 다음과 같이 간단하게 프로토콜을 정의했습니다. 이 프로토콜은 사용자가 변경해도 되고 로봇 기능이 추가되면 프로토콜도 추가할 수 도 있습니다. 이 코드는 유선 시리얼 포트로 로봇 제어하는 코드와 동일합니다.  

|로봇 동작|문자 코드|
|------|---|
|전진|'f'|
|후진|'b'|
|좌회전|'l'|
|우회전|'r'|
|멈춤|'p'|

그리고 이것을 처리하는 코드는 다음과 같습니다. 스마트폰에 동작을 확인시켜주기 위해서 SerialBT.println() 함수를 사용하여 메시지를 스마트폰을 보냈습니다. 

```C
 if(c == 'f'){
      SerialBT.println("forward");
      go_forward();
    }else if(c == 'b'){
      SerialBT.println("backward");
      go_backward();
    }else if(c == 'l'){
      SerialBT.println("turn left");
      turn_left();
    }else if(c == 'r'){
      SerialBT.println("turn right");
      turn_right(); 
    }else if(c == 'p'){
      stop();
    }
```

### 스마트폰얍을 통한 4바퀴 로봇의 제어 
이제 스마트폰앱을 통해서 4바퀴로봇을 제어해 보갰습니다. 먼저 다비이스를 스마트폰에 등록을 해야 합니다. 그 다음에는 스마트폰에서 Serial Bluetooth Terminal 앱을 실행합니다. 앱을 실행하면 다음과 같은 화면이 뜨게 됩니다.  

